namespace about
    "Google GDrive about namespace"

import gdrive

struct UserInfo
    displayName String
        "A plain text displayable name for this user."
    kind String = "drive#user"
        "This is always drive#user."
    photoLink String
        "A link to the user's profile photo, if available."
    me Boolean
        "Whether this user is the requesting user."
    permissionId String
        "The user's ID as visible in Permission resources."
    emailAddress String
        "The email address of the user. This may not be present in certain contexts if the user has not made their email address visible to the requester."

struct StorageQuota
    limit Float64
        "The usage limit, if applicable. This will not be present if the user has unlimited storage."
    usage Float64
        "The total usage across all services."
    usageInDrive UInt64
        "The usage by all files in Google Drive."
    usageInDriveTrash UInt64
        "The usage by trashed files in Google Drive."
    maxUploadSize UInt64
        "The maximum upload size in bytes." 
    appInstalled Boolean
        "Whether the user has installed the requesting app."


struct AboutResource
    kind String = "drive#about"
        "This is always drive#about."
    user UserInfo
        "The authenticated user."
    storageQuota StorageQuota
        "storage quota"

route get (gdrive.AboutArg, AboutResource, Void)
    "Gets information about the user, the user's Drive, and system capabilities."
    attrs
        style = "getDrive"


